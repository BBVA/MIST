Program:
  commands*=Command
;

Command:
  IncludeCommand |PrintCommand | AbortCommand | SaveListCommand | SaveCommand |
  DataCommand | CheckCommand | IterateCommand | WatchCommand | FunctionDefinition |
  SetCommand | ExposeCommand | EndCommand | AppendCommand | FunctionCall | SendCommand |
  ReturnCommand
;

IncludeCommand:
    'include' files+=STRING[eolterm]
;

PrintCommand:
    'print' texts+=IDorSTRING[eolterm]
;

AbortCommand:
    'abort' (reason=STRING)?
;

DataCommand:
  'data' name=ID '{'
    params*=ID
  '}'
;

Param:
    key=ID '<=' value=IDorSTRING
;

AppendCommand:
  'append' target=ID '<=' value=IDorSTRING
;

CustomList:
  '['(components+=IDorSTRING[','])? ']'
;

NamedArg:
    key=ID '=' value=IDorSTRING
;

FunctionInlineCall:
  name=ID ('()' | '(' (namedArgs+=NamedArg[','] | args+=IDorSTRING[',']) ')')
;

StringData:
  data=/(?ms)\'{3}(.*?)\'{3}/
;

VarReference:
  id=ID('.'childs+=ID['.'])?
;

ExtParameter:
  '%' param=ID
;

EnvVariable:
  '$' var=ID
;

Source:
  ':' source=ID
;

IDorSTRING:
  value=FunctionInlineCall | value=CustomList | value=StringData | value=VarReference |
  value=STRING | value=INT | value=ExtParameter | value=EnvVariable | value=Source
;

FunctionCall:
  (key=ID '=')? name=ID ('()' | '(' (namedArgs+=NamedArg[','] | args+=IDorSTRING[',']) ')') ('{'
      commands*=Command
    '}')? ('=>' targetStream=ID)?
;

FunctionDefinition:
  'function' name=ID '(' args*=ID[','] ')' '{'
    commands*=Command
  '}'
;

ReturnCommand:
  'return' value=IDorSTRING
;

OutputId:
  source=ID ('(' fields+=ID ')' '=>' target=ID)?
;

SaveListCommand:
  'putlist' list=ID ('(' selectors+=ID ')')? sources*=IDorSTRING '=>' target=ID ('(' params*=ID ')')?
;

SaveCommand:
  'put' sources+=IDorSTRING '=>' target=ID ('(' params*=ID ')')?
;

WatchCommand:
  'watch' var=ID '=>' name=ID '{'
    commands*=Command
  '}'
;

CheckCommand:
  'check' var=IDorSTRING operator=CheckOperator result=IDorSTRING '{'
    commands+=Command
  '}' ('else' '{' elseCommands+=Command '}')?
;

CheckOperator:
  'is not' | 'is'
;

IterateCommand:
  'iterate' var=IDorSTRING '=>' name=ID '{'
    commands*=Command
  '}'
;

SetCommand:
  'set' key=ID '<=' value=IDorSTRING
;

SendCommand:
  'send' value=IDorSTRING
;

ExposeCommand:
  'expose' value=ID
;

EndCommand:
  'done'
;

Comment:
  /#.*$/
;
