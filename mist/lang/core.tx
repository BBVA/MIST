Program:
  commands*=Command
;

Command:
  IncludeCommand | SaveListCommand | DataCommand | CheckCommand |
  WatchCommand | FunctionDefinition | SetCommand | EndCommand |
  AppendCommand | FunctionCall | ReturnCommand
;

IncludeCommand:
    'include' files+=STRING[eolterm]
;

DataCommand:
  'data' name=ID '{'
    params*=ID
  '}'
;

Param:
    key=ID '<=' value=IDorSTRING
;

AppendCommand:
  'append' target=ID '<=' value=IDorSTRING
;

CustomList:
  '['(components+=IDorSTRING[','])? ']'
;

CustomDict:
  '{' (entries+=DictEntry[','])? '}'
;

DictEntry:
  key=STRING ':' value=IDorSTRING
;

NamedArg:
    key=ID '=' value=IDorSTRING
;

StringData:
  data=/(?ms)\'{3}(.*?)\'{3}/
;

VarReference:
  id=ID('.'childs+=ID['.'])?
;

ListReference:
  id=ID '[' index=INT ']'
;

DictReference:
  id=ID '[' key=STRING ']'
;

ExtParameter:
  '%' param=ID
;

EnvVariable:
  '$' var=ID
;

Source:
  ':' source=ID
;

IDorSTRING:
  value=ListReference | value=DictReference | value=FunctionCall | value=CustomList |
  value=CustomDict | value=StringData | value=VarReference | value=STRING |
  value=INT | value=ExtParameter | value=EnvVariable | value=Source
;

SetCommand:
  key=ID '=' value=IDorSTRING
;

FunctionCall:
  name=ID ('()' | '(' (namedArgs+=NamedArg[','] | args+=IDorSTRING[',']) ')') ('{'
      commands*=Command
    '}')? ('=>' targetStream=ID)?
;

FunctionDefinition:
  'function' name=ID '(' args*=ID[','] ')' '{'
    commands*=Command
  '}'
;

ReturnCommand:
  'return ' (value=IDorSTRING)? | 'return'
;

OutputId:
  source=ID ('(' fields+=ID ')' '=>' target=ID)?
;

SaveListCommand:
  'putlist' list=ID ('(' selectors+=ID ')')? sources*=IDorSTRING '=>' target=ID ('(' params*=ID ')')?
;

WatchCommand:
  'watch' var=ID '=>' name=ID '{'
    commands*=Command
  '}'
;

CheckCommand:
  'check' var=IDorSTRING operator=CheckOperator result=IDorSTRING '{'
    commands+=Command
  '}' ('else' '{' elseCommands+=Command '}')?
;

CheckOperator:
  'is not' | 'is'
;

EndCommand:
  'done'
;

Comment:
  /#.*$/
;
